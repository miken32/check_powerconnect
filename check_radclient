#!/usr/bin/perl -w

# check_radclient
# Copyright (C) 2019 Michael Newton
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

use POSIX;
use strict;
use Getopt::Long qw(:config no_ignore_case);
use Time::HiRes qw(gettimeofday tv_interval);

my $script = "check_radclient";
my $version = "2.0";

# default values
my $request_type;
my $host = "localhost";
my $port = 1812;
my $ipmode = 4;
my $timeout = 10;
my $secret = "";
my %avpairs = ("Message-Authenticator" => "0x00");
my $radclient_binary = "/usr/bin/radclient";
my $retries = 1;
my $send_count = 1;
my $warn_thresh = 3;
my $crit_thresh = 7;
my $debug = 0;

# misc variables
my $command = "";
my $t0;
my $elapsed;
my @avstrings;
my $avstring;
my $errindex;
my %ERRORS = ('OK'=>0,'WARNING'=>1,'CRITICAL'=>2,'UNKNOWN'=>3,'DEPENDENT'=>4);

GetOptions (
    "f|function=s" => \$request_type,
    "H|host:s" => \$host,
    "p|port:1812" => \$port,
    "6|ipv6" => sub{ $ipmode = 6 },
    "t|timeout:8" => \$timeout,
    "s|secret:s" => \$secret,
    "a|avpair:s" => \%avpairs,
    "c|client:s" => \$radclient_binary,
    "r|retries:1" => \$retries,
    "u|count:1" => \$send_count,
    "W|warn:3" => \$warn_thresh,
    "C|crit:7" => \$crit_thresh,
    "d|debug" => \$debug,
    "V|version" => sub{ printf("$version\n"); exit 1; },
    "help" => sub{ show_usage(); },
);

if (!$request_type) {
    show_usage(2);
}

while (my($key, $val) = each %avpairs) {
    push(@avstrings, "$key=$val");
}
$avstring = join(",", @avstrings);

if ($debug) {
    print STDERR sprintf(
        "DEBUG: Parsed config: request_type = %s, host = %s:%d (IPv%d), timeout = %d, secret = %s, avpair = \"%s\", radclient_binary = %s, warn_thresh = %d, crit_thresh = %d, debug = %d\n",
        $request_type, $host, $port, $ipmode, $timeout, $secret, $avstring, $radclient_binary, $warn_thresh, $crit_thresh, $debug
    );
}

if ( $request_type eq "auth" || $request_type eq "acct" || $request_type eq "status") {
    $command = "printf '%q' '$avstring' | $radclient_binary -$ipmode -q -c $send_count -r $retries -t $timeout $host:$port $request_type $secret";
    if ($debug) {
        print STDERR sprintf("DEBUG: radclient command to send: %s\n", $command);
    }
} else {
    print STDERR sprintf("ERROR: Unknown function %s passed, must be one of auth, acct, or status.", $request_type);
    show_usage(2);
}

$t0 = [gettimeofday()];
system($command);
$elapsed = tv_interval($t0);

if ($elapsed >= $crit_thresh || $? != 0) {
    $errindex = "CRITICAL";
} elsif ($elapsed < $warn_thresh && $? == 0) {
    $errindex = "OK";
} elsif ($elapsed >= $warn_thresh && $? == 0) {
    $errindex = "WARNING";
} else {
    $errindex = "UNKNOWN";
}

printf(
    "%s: Radius response time: %d secs, warning threshold: %d, critical threshold: %d, radclient exit code: %d, %s exit code: %s.",
    $errindex, $elapsed, $warn_thresh, $crit_thresh, $? >> 8, $script, $ERRORS{$errindex}
);
exit $ERRORS{$errindex};

sub show_usage
{
    print << "USAGE"; 
\n$script $version
Usage: $script [OPTIONS]

 Operational mode:
    -f, --function=request_type

 request_type is mandatory and must be one of the following:
    auth                    test authentication (Access-Request)
    acct                    test accounting (Accounting-Request)
    stat                    test server status (Server-Status)

 Server options:
    -H, --host=SERVER       connect to SERVER (default localhost)
    -p, --port=port         connect to UDP port port (default 1812)
    -6, --ipv6              connect using IPv6
    -s, --secret=SECRET     use shared secret SECRET (default is empty string)
    -t, --timeout=SECS      timeout after SECS seconds (default 10)
    -r, --retries=NUM       retry NUM times on failure or timeout (default 1)
    -u, --count=NUM         send each packet NUM times (default 1)

 Request options:
    -a, --avpair=ATTR=VAL   define attribute/value pairs for the request
                            (default Message-Authenticator=0x00)

 Other options:
    -c, --client=CLIENT     path to radclient if needed
    -W, --warn=SECS         return warning if request takes more than SECS s
                            (default 3)
    -C, --critical=SECS     return critical if request takes more than SECS s
                            (default 7)
    -h, --help              show usage information
    -d, --debug             enable debugging output

Examples:

    ./$script -f auth --host 10.20.30.40 --port 1812 -t 8 -s testing123 \
        -a User-Name=joe -a User-Password=pass -a NAS-IP-Address=192.168.0.9 \
        -c /usr/local/freeradius/bin/radclient -W 5 -C 10 -d

    ./$script --function status --host 172.16.12.6 --port 1812 --timeout 8 \
        --secret 'secret!' --client /usr/local/bin/radclient --crit 8 --debug

    ./$script --help

USAGE
    my ($err) = @_;
    $err ||= 1;
    exit $err;
}

